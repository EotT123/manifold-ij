plugins {
  id 'org.jetbrains.intellij' version '0.2.17'
}

description = 'Manifold :: IJ'

wrapper {
  gradleVersion '4.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
  compile group: 'systems.manifold', name: 'manifold', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-ext', version: manifoldVersion
}

compileJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold']
}

afterEvaluate {
  task downloadDependencies(type: Exec) {
    configurations.testRuntime.files
    commandLine 'echo', 'Downloaded all dependencies'
  }
}

intellij {
  pluginName project.name
  version '2017.1.5' // Ordinarily, just use the version number: '15.0.6' or '2016.3'. Alternatively, the precise API level is OK too: 'IC-143.2370.31'
  plugins = ['ASM Bytecode Outline:0.3.5'] //include ASM in the sandbox by default. Nice.

  downloadSources true

  //if running on CI, don't download sources
  if(System.getenv('CI') != null) {
    downloadSources false
  }
}

patchPluginXml {
  sinceBuild '141.1010'
  untilBuild '999.*'
  pluginDescription   '<![CDATA[\n' +
                      '<b><a href="http://manifold.systems/">Manifold</a> support for IntelliJ IDEA.</b><br>' +
                      '<br>' +
                      '<p>At its core <a href="https://manifold.systems/">Manifold</a> is a unique framework to dynamically and seamlessly extend\n' +
                      'Java\'s type system. Building on this core framework Manifold provides a set of\n' +
                      'key features found in other programming languages and platforms:</p>\n' +
                      '<ul>\n' +
                      '<li><strong>Type-safe Metaprogramming</strong> -- similar in concept to <a href="https://www.microsoft.com/en-us/research/wp-content/uploads/2016/02/information-rich-themes-v4.pdf">F# <em>type providers</em></a></li>\n' +
                      '<li><strong>Extension Methods</strong> -- comparable to the same feature in <a href="https://kotlinlang.org/docs/reference/extensions.html">Kotlin</a> and <a href="https://docs.microsoft.com/en-us/dotnet/csharp/programming-guide/classes-and-structs/extension-methods">C#</a></li>\n' +
                      '<li><strong>Structural Typing</strong> -- much like interfaces in <a href="https://www.typescriptlang.org/docs/handbook/interfaces.html">TypeScript</a> and <a href="https://tour.golang.org/methods/10">Go</a></li>\n' +
                      '</ul>\n' +
                      '<p>Leveraging these key features Manifold delivers a powerful set of Java extensions including <strong>JSON</strong>\n' +
                      'integration, <strong>JavaScript</strong> interop, <strong>Structural typing</strong>, seamless <strong>extension libraries</strong> to Java\'s\n' +
                      'runtime classes, and (coming soon) type-safe access to raw <strong>SQL</strong> and <strong>DDL</strong>.</p>' +
                      ']]>'
}

publishPlugin {
  username = System.getenv()['JB_PUBLISH_USER']
  password = System.getenv()['JB_PUBLISH_PASS']
}
